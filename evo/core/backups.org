* History
** Backups
   There's a very compelling [[https://stackoverflow.com/a/20824625/794944][SO answer]] (and accompanying [[https://www.emacswiki.org/emacs/ForceBackups][ewiki page]])
   which provides a really neat and quite robust backup strategy.

   First, we set up versioned backups, which will keep 10 backups,
   ought to be just about enough history for anyone. You can of course
   tweak this if you find you need more.

   #+BEGIN_SRC emacs-lisp
     (use-package files
       :init
       (setq version-control t
	     kept-new-versions 10
	     kept-old-versions 0
	     delete-old-versions t
	     backup-by-copying t))
   #+END_SRC
  
   Emacs evidently doesn't backup version-controlled files by default,
   so let's fix that. If you've never accidentally wiped all your work
   between commits, we must be in different universes.

   #+BEGIN_SRC emacs-lisp
     (use-package vc-hooks
       :init
       (setq vc-make-backup-files t))
   #+END_SRC

   Finally, we'll set up a two-pronged backup strategy -- per-save, and
   per-session. First, we want to set up a directory for per-save
   backups.

   #+BEGIN_SRC emacs-lisp
     (use-package files
       :init
       (progn
	 (setq backup-directory (f-join user-emacs-directory "backup"))
	 (setq backup-directory-alist
	       `(("" . ,(f-join
			 backup-directory "per-save"))))))
   #+END_SRC

   Then, we hook into the =before-save= to save a per-session backup
   (analogous to Emacs' usual backup strategy), then force a per-save
   backup by unsetting =buffer-backed-up=.

   #+BEGIN_SRC emacs-lisp
     (use-package files
       :init
       (progn
	 (defun force-backup-of-buffer ()
	   (when (not buffer-backed-up)
	     (let ((backup-directory-alist
		    `(("" . ,(f-join
			      backup-directory "per-session"))))
		   (kept-new-versions 3))
	       (backup-buffer)))
	   (let ((buffer-backed-up nil))
	     (backup-buffer)))

	 (add-hook 'before-save-hook 'force-backup-of-buffer)))
   #+END_SRC

** Command History
   It's nice to be able to refer back to previous commands (and it's
   utterly necessary for extended commands in Evil).

   #+BEGIN_SRC emacs-lisp
     (use-package savehist
       :after files
       :init
       (progn
	 (setq savehist-file (concat backup-directory "savehist")
	       enable-recursive-minibuffers t
	       history-length 1000
	       savehist-additional-variables '(mark-ring
					       global-mark-ring
					       search-ring
					       regexp-search-ring
					       extended-command-history)
	       savehist-autosave-interval 60)
	 (savehist-mode t)))
   #+END_SRC
